plugins {
	// Using SNAPSHOT as stable releases don't yet support Gradle 9.x
	// This will be updated to stable release when available
	id 'fabric-loom' version '1.11-SNAPSHOT'
	id 'maven-publish'
}

base {
	archivesName = "incognito-1.21.9-1.21.10"
}

loom {
	splitEnvironmentSourceSets()
	
	mods {
		"incognito" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
	
	mixin {
		useLegacyMixinAp = true
		defaultRefmapName = "incognito.refmap.json"
	}
}

sourceSets {
	main {
		java.srcDir rootProject.file('src/main/java')
		resources.srcDir rootProject.file('src/main/resources')
	}
	client {
		java.srcDir rootProject.file('src/client/java')
		resources.srcDir rootProject.file('src/client/resources')
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
}

processResources {
	inputs.property "version", project.version
	
	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName
	
	from("../LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = "incognito-modern"
			from components.java
		}
	}
}
