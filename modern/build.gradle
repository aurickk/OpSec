plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

base {
	archivesName = "opsec-${project.version_suffix}"
}

loom {
	splitEnvironmentSourceSets()
	
	mods {
		"opsec" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
	
	mixin {
		useLegacyMixinAp = true
		defaultRefmapName = "opsec.refmap.json"
	}
}

sourceSets {
	main {
		java.srcDirs = [rootProject.file('src/main/java')]
		// Include BOTH shared resources (icon, lang) and subproject-specific (fabric.mod.json)
		resources.srcDirs = [rootProject.file('src/main/resources'), file('src/main/resources')]
	}
	client {
		// Include BOTH shared sources and subproject-specific sources
		java.srcDirs = [rootProject.file('src/client/java'), file('src/client/java')]
		resources.srcDirs = [rootProject.file('src/client/resources')]
	}
}

repositories {
	maven { url = "https://maven.bawnorton.com/releases" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
	
	// MixinSquared for cancelling other mods' mixins (Meteor Client fix)
	include(implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:0.3.2-beta.4")))
}

processResources {
	inputs.property "version", project.version
	inputs.property "minecraft_version_range", project.minecraft_version_range
	
	filesMatching("fabric.mod.json") {
		expand([
			"version": project.version,
			"minecraft_version_range": project.minecraft_version_range
		])
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName
	
	from("../LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = "opsec-modern"
			from components.java
		}
	}
}
